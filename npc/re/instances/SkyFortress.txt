//===== rAthena Script =======================================
//= Sky Fortress Invasion
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Sky Fortress Invasion Instance
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

prt_q,249,82,2	script	Han#a1	8W_SOLDIER,{
	mes "[Han]";
	mes "There must be a ton of monsters";
	mes "attacking the center of Prontera.";
	mes "***";
	next;
	mes "[Han]";
	mes "My family...My friends...";
	mes "They're all there...";
	mes "I must protect them...";
	mes "But...I have an even more important";
	mes "job to do here...";
	next;
	mes "[Han]";
	mes "I, scientist Doyeon, can do it.";
	mes "I can obliterate that dirty source place,";
	mes "ceaselessly spawning the monsters...";
	mes "That's why I'm protecting him.";
	mes "***";
	next;
	mes "[Han]";
	mes "Protecting him and destroying this fortress";
	mes "is what ultimately protects my family";
	mes "and friends.";
	close;
}

prt_q,252,79,4	script	Doek#a1	8W_SOLDIER,{
	mes "[Doek]";
	mes "Can this scientist you're seeing here";
	mes "really protect Prontera?";
	mes "Can she?";
	mes "Do you believe so?";
	next;
	mes "[Doek]";
	mes "Honestly, I don't...";
	mes "But...";
	next;
	mes "[Doek]";
	mes "All my loved ones were...";
	mes "...by those monsters....";
	next;
	mes "[Doek]";
	mes "This is the last hope I can have...";
	mes "This is the last.";
	mes "So please help me.";
	mes "Help Doyeon...";
	next;
	mes "- And then, she remained silent -";
	mes "- for quite a while. -";
	close;
}

prt_q,249,79,4	script	Scientist Doyeon#a1	4_M_MAYOR,{
	if (BaseLevel < 145) {
		mes "- You should be level 145 or higher";
		mes "to proceed. -";
		close;
	}
	switch( checkquest(9419,PLAYTIME) ) {
	case -1:
		if (getcharid(1) == 0 || getpartyleader(getcharid(1),2) != getcharid(0)) {
			callsub S_Info;
			close;
		}
		if (isbegin_quest(9418) == 0) {// first entrance
			mes "[Scientist Doyeon]";
			mes "I'm a scientist";
			mes "living in Prontera.";
			mes "I'm just an ordinary scientist";
			mes "teaching children and doing research.";
			mes "And then...things came up";
			mes "in Prontera all of a sudden....";
			while(true) {
				next;
				switch( select( "About Sky Fortress", "Enter the Sky Fortress.", "End the dialogue." ) ) {
				case 1:
					callsub S_Info;
					break;
				case 2:
					mes "[Scientist Doyeon]";
					mes "I think I can open the warp,";
					mes "at least for once, with the magic";
					mes "that I've been collecting so hard.";
					mes "I hope you would help me";
					mes "destroy the fortress and";
					mes "get back our Prontera.";
					next;
					if (select( "No", "Yes" ) == 1) {
						mes "[Scientist Doyeon]";
						mes "Come back";
						mes "if you change your mind...";
						mes "Just know that time is running out...";
						break;
					}
					mes "[Scientist Doyeon]";
					mes "There! I opened the warp";
					mes "to the Sky Fortress!";
					mes "The warp will be activated";
					mes "only for a limited period of time.";
					mes "Step inside it, quick!";
					instance_create("Sky Fortress Invasion");
					close;
				case 3:
					mes "[Scientist Doyeon]";
					mes "I'm so worried about";
					mes "the future of Prontera...";
					mes "It appears this fortress";
					mes "must be the source of all evil...";
					close;
				}
			}
		}
		mes "You're back unscathed.";
		mes "So...whatever happened";
		mes "inside?";
		next;
		mes "- I told her everything that";
		mes "happened inside the Sky Fortress. -";
		next;
		mes "[Scientist Doyeon]";
		mes "How could that be...";
		mes "Still, I think we have every reason to remain hopeful";
		mes "for eliminating that huge Golem";
		mes "in the Sky Fortress.";
		next;
		mes "[Scientist Doyeon]";
		mes "Still, those filthy monsters";
		mes "keep spawning in Prontera.";
		mes "So I think what we've done";
		mes "must not be enough.";
		mes "***";
		next;
		mes "[Scientist Doyeon]";
		mes "To operate the warp,";
		mes "a lot of magic is required.";
		mes "Bring me the item called the -Dungeon Pass-,";
		mes "or I need about";
		mes "three days to collect magic.";
		next;
		mes "[Scientist Doyeon]";
		mes "I hope you'd enter the Sky Fortress";
		mes "and help us save";
		mes "Prontera.";
		completequest 9418;// Attack Sky Fortress Invading Prontera
		setquest 9419;// Attack Sky Fortress Invading Prontera
		if (isbegin_quest(9427) == 0) {
			setquest 9427;// Clearing the Sky Fortress for the Same Time
			completequest 9427;
			rentitem 14505,3600;// Sky Fortress Ticket
		}
		close;
	case 0:
	case 1:
		mes "[Scientist Doyeon]";
		mes "To operate the warp,";
		mes "a lot of magic is required.";
		mes "Bring me the item called the -Dungeon Pass-,";
		mes "or I need about";
		mes "three days to collect magic.";
		next;
		if (getcharid(1) > 0 && getpartyleader(getcharid(1),2) == getcharid(0)) {
			if (select( "End the dialogue.", "Show the -Dungeon Pass-." ) == 2) {
				if (countitem(14505) < 1 && countitem(14506) < 1) {// Sky Fortress Ticket
					mes "[Scientist Doyeon]";
					mes "I don't think this works";
					mes "without the -Dungeon Pass-.";
					mes "I can't open the warp without it.";
					close;
				}
				mes "[Scientist Doyeon]";
				mes "Oh! Where did you";
				mes "get the -Dungeon Pass-?";
				mes "With this item,";
				mes "I can pretty much open the warp.";
				next;
				if (select( "End the dialogue.", "Activate the warp." ) == 1) {
					mes "[Scientist Doyeon]";
					mes "Are you not going";
					mes "to help Prontera?";
					close;
				}
				mes "[Scientist Doyeon]";
				mes "There! I opened the warp";
				mes "to the Sky Fortress!";
				mes "The warp will be activated";
				mes "only for a limited period of time.";
				mes "Step inside it, quick!";
				instance_create("Sky Fortress Invasion");
				close;
			}
		}
		mes "[Scientist Doyeon]";
		mes "I hope you'd enter the Sky Fortress";
		mes "and help us save";
		mes "Prontera.";
		close;
	case 2:
		mes "[Scientist Doyeon]";
		mes "You've come back! You didn't get away!";
		mes "Thanks so much!";
		next;
		mes "[Scientist Doyeon]";
		mes "But still...";
		mes "A ton of undead monsters kept swarming about";
		mes "from that entrance seen behind me";
		mes "as if the sap monster spits out the sap.";
		mes "They kept coming out";
		mes "no matter how many were killed.";
		next;
		mes "[Scientist Doyeon]";
		mes "I suppose the monsters are being made up";
		mes "inside the fortress.";
		mes "My guess is that if we can defeat their ringleader in the fortress,";
		mes "the monsters will go away";
		mes "and Prontera will become stabilized.";
		mes "***";
		next;
		mes "[Scientist Doyeon]";
		mes "I think I can open the warp,";
		mes "at least for once, with the magic";
		mes "that I've been collecting so hard.";
		mes "I hope you would help me";
		mes "destroy the fortress and";
		mes "get back our Prontera.";
		if (getcharid(1) > 0 && getpartyleader(getcharid(1),2) == getcharid(0)) {
			next;
			if (select( "No", "Yes" ) == 1) {
				mes "[Scientist Doyeon]";
				mes "Come back";
				mes "if you change your mind...";
				mes "Just know that time is running out...";
				close;
			}
			mes "[Scientist Doyeon]";
			mes "There! I opened the warp";
			mes "to the Sky Fortress!";
			mes "The warp will be activated";
			mes "only for a limited period of time.";
			mes "Step inside it, quick!";
			instance_create("Sky Fortress Invasion");
		}
		close;
	}
	end;

S_Info:
	mes "[Scientist Doyeon]";
	mes "I'm a scientist";
	mes "living in Prontera.";
	mes "I'm just an ordinary scientist";
	mes "teaching children and doing research.";
	next;
	mes "[Scientist Doyeon]";
	mes "But now...the life at least as I know it";
	mes "has become shattered.";
	next;
	mes "[Scientist Doyeon]";
	mes "Thanks to this fortress";
	mes "looking like a huge trash can,";
	mes "our village has turned into a trash heap";
	mes "that doesn't even decay.";
	next;
	mes "[Scientist Doyeon]";
	mes "Buildings fall down,";
	mes "people keep dying out...";
	mes "I have to wonder what the nobility and soldiers at the Prontera Palace";
	mes "are up to when things are like this.";
	next;
	mes "[Scientist Doyeon]";
	mes "A ton of undead monsters kept swarming about";
	mes "from that entrance seen behind me";
	mes "as if the sap monster spits out the sap.";
	mes "They kept coming out";
	mes "no matter how many were killed.";
	next;
	mes "[Scientist Doyeon]";
	mes "I suppose the monsters are being made up";
	mes "inside the fortress.";
	mes "My guess is that if we can defeat their ringleader in the fortress,";
	mes "the monsters will get away";
	mes "and Prontera will become stabilized.";
	mes "***";
	next;
	mes "[Scientist Doyeon]";
	mes "So I really wanted to enter the fortress";
	mes "but couldn't find any entrance.";
	mes "I studied day and night";
	mes "and finally located it.";
	next;
	mes "[Scientist Doyeon]";
	mes "To enter the fortress,";
	mes "the warp should be activated.";
	mes "To do so, a lot of magic";
	mes "is indispensable!";
	next;
	mes "[Scientist Doyeon]";
	mes "And then...I've been preparing";
	mes "a lot of magic necessary for the warp.";
	mes "Now I could use a warrior";
	mes "who can shatter the fortress";
	mes "and bring peace to Prontera.";
	return;
}

prt_q,243,75,3	script	Fortress Entry Warp Portal#1	PORTAL,{
	if (BaseLevel < 145) {
		mes "- You should be level 145 or higher";
		mes "to proceed. -";
		close;
	}
	if ((checkquest(9419,PLAYTIME) == -1 || checkquest(9419,PLAYTIME) == 2) && isbegin_quest(9418) != 1)
		setarray .@menu$[0], "Don't step into the warp.", "Step into the warp.";
	else {
		mes "- The warp doesn't seem to work.";
		mes "You can't seem to enter... -";
		next;
		setarray .@menu$[0], "End the dialogue.", "", "Put the - Dungeon Pass - near to the warp.";
	}
	switch( select( .@menu$[0], .@menu$[1], .@menu$[2] ) ) {
	case 1:
		mes "- The warp is emanating bright lights. -";
		close;
	case 2:
		break;
	case 3:
		if (countitem(14505) < 1 && countitem(14506) < 1) {// Sky Fortress Ticket
			mes "- You don't have -";
			mes "- the Dungeon Pass. -";
			close;
		}
		break;
	}
	.@md_name$ = "Sky Fortress Invasion";
	switch( instance_enter(.@md_name$) ) {
	case IE_OTHER:
		mes "An unknown error occurred.";
		close;
	case IE_NOINSTANCE:
		mes "Memorial Dungeon " + .@md_name$ + " doesn't exist.";
		mes "Party leader hasn't created the Memorial Dungeon.";
		close;
	case IE_NOMEMBER:
		mes "Only the party members may enter the Memorial Dungeon.";
		close;
	case IE_OK:
		mapannounce "prt_q", "Party member " + strcharinfo(0) + " of party's " + getpartyname( getcharid(1) ) + " enters " + .@md_name$ + "", bc_map,0xFF99;
		switch( checkquest(9419,PLAYTIME) ) {
		case -1:
			switch( isbegin_quest(9418) ) {
			case 0:
				setquest 9418;// Attack Sky Fortress Invading Prontera
				break;
			case 1:
				break;
			case 2:
				erasequest 9418;
				setquest 9418;// Attack Sky Fortress Invading Prontera
				break;
			}
			break;
		case 0:
		case 1:
			break;
		case 2:
			erasequest 9419;
			switch( isbegin_quest(9418) ) {
			case 0:
				setquest 9418;// Attack Sky Fortress Invading Prontera
				break;
			case 1:
				break;
			case 2:
				erasequest 9418;
				setquest 9418;// Attack Sky Fortress Invading Prontera
				break;
			}
			break;
		}
		// warp "1@sthb",54,67;
		end;
	}
	end;

OnInit:
	while(true) {
		sleep 10000;
		specialeffect EF_ENHANCE;
	}
	end;
}

// Dali entrance
dali02,115,61,3	script	Fortress Entry Warp Portal#2	PORTAL,{
	if (BaseLevel < 145) {
		mes "- You should be level 145 or higher";
		mes "to proceed. -";
		close;
	}
	mes "- The warp doesn't seem to work.";
	mes "You can't seem to enter. -";
	next;
	if (select( "End the dialogue.", "Put the - Dungeon Pass - near to the warp." ) == 1) {
		mes "- The warp is emanating bright lights. -";
		close;
	}
	if (countitem(14505) < 1 && countitem(14506) < 1) {// Sky Fortress Ticket
		mes "- You don't have -";
		mes "- the Dungeon Pass. -";
		close;
	}
	.@md_name$ = "Sky Fortress Invasion";
	switch( instance_enter(.@md_name$) ) {
	case IE_OTHER:
		mes "An unknown error occurred.";
		close;
	case IE_NOINSTANCE:
		mes "Memorial Dungeon " + .@md_name$ + " doesn't exist.";
		mes "Party leader hasn't created the Memorial Dungeon.";
		close;
	case IE_NOMEMBER:
		mes "Only the party members may enter the Memorial Dungeon.";
		close;
	case IE_OK:
		mapannounce "prt_q", "Party member " + strcharinfo(0) + " of party's " + getpartyname( getcharid(1) ) + " enters " + .@md_name$ + "", bc_map,0xFF99;
		switch( checkquest(9419,PLAYTIME) ) {
		case -1:
			switch( isbegin_quest(9418) ) {
			case 0:
				setquest 9418;// Attack Sky Fortress Invading Prontera
				break;
			case 1:
				break;
			case 2:
				erasequest 9418;
				setquest 9418;// Attack Sky Fortress Invading Prontera
				break;
			}
			break;
		case 0:
		case 1:
			break;
		case 2:
			erasequest 9419;
			switch( isbegin_quest(9418) ) {
			case 0:
				setquest 9418;// Attack Sky Fortress Invading Prontera
				break;
			case 1:
				break;
			case 2:
				erasequest 9418;
				setquest 9418;// Attack Sky Fortress Invading Prontera
				break;
			}
			break;
		}
		// warp "1@sthb",54,67;
		end;
	}

OnInit:
	while(true) {
		sleep 10000;
		specialeffect EF_ENHANCE;
	}
	end;
}

dali02,122,63,2	script	Scientist Doyeon#a2	4_M_MAYOR,{
	if (BaseLevel < 145) {
		mes "- You should be level 145 or higher";
		mes "to proceed. -";
		close;
	}
	if (getcharid(1) < 1 || getcharid(0) != getpartyleader(getcharid(1),2)) {
		mes "[Scientist Doyeon]";
		mes "I'm a scientist";
		mes "living in Prontera.";
		mes "I'm just an ordinary scientist";
		mes "teaching children and doing research.";
		next;
		mes "[Scientist Doyeon]";
		mes "But now...my normal life";
		mes "has become shattered.";
		next;
		mes "[Scientist Doyeon]";
		mes "Thanks to this fortress";
		mes "looking like a huge trash can,";
		mes "our village has turned into a trash heap";
		mes "that doesn't even decay.";
		next;
		mes "[Scientist Doyeon]";
		mes "Buildings fall down,";
		mes "people keep dying out...";
		mes "I have to wonder what the nobility and soldiers at the Prontera Palace";
		mes "are up to when things are like this.";
		next;
		mes "[Scientist Doyeon]";
		mes "A ton of undead monsters kept swarming about";
		mes "from that entrance seen behind me";
		mes "as if the sap monster spits out the sap.";
		mes "They kept coming out";
		mes "no matter how many were killed.";
		next;
		mes "[Scientist Doyeon]";
		mes "I suppose the monsters are being made up";
		mes "inside the fortress.";
		mes "My guess is that if we can defeat their ringleader in the fortress,";
		mes "the monsters will go away";
		mes "and Prontera will become stabilized.";
		mes "***";
		next;
		mes "[Scientist Doyeon]";
		mes "So I really wanted to enter the fortress";
		mes "but couldn't find any entrance.";
		mes "I studied day and night";
		mes "and finally located it.";
		next;
		mes "[Scientist Doyeon]";
		mes "To enter the fortress,";
		mes "the warp should be activated.";
		mes "To do so,";
		mes "the -Dungeon Pass-";
		mes "is indispensable!";
		next;
		mes "[Scientist Doyeon]";
		mes "And then...I've been preparing";
		mes "a lot of magic necessary for the warp.";
		mes "Now I could use a warrior";
		mes "who can shatter the fortress";
		mes "and bring peace to Prontera.";
		close;
	}
	mes "[Scientist Doyeon]";
	mes "I'm a scientist";
	mes "living in Prontera.";
	mes "I teach children and perform research activities.";
	mes "But it has become";
	mes "so dangerous there,";
	mes "so I moved in here.";
	next;
	switch( select( "About Sky Fortress", "Enter the Sky Fortress.", "End the dialogue." ) ) {
	case 1:
		mes "[Scientist Doyeon]";
		mes "But now...my normal life";
		mes "has become shattered.";
		next;
		mes "[Scientist Doyeon]";
		mes "Thanks to this fortress";
		mes "looking like a huge trash can,";
		mes "our village has turned into a trash heap";
		mes "that doesn't even decay.";
		next;
		mes "[Scientist Doyeon]";
		mes "Buildings fall down,";
		mes "people keep dying out...";
		mes "I have to wonder what the nobility and soldiers at the Prontera Palace";
		mes "are up to when things are like this.";
		next;
		mes "[Scientist Doyeon]";
		mes "A ton of undead monsters kept swarming about";
		mes "as if the sap monster spits out the sap.";
		mes "They kept coming out";
		mes "no matter how many were killed.";
		mes "***";
		next;
		mes "[Scientist Doyeon]";
		mes "I suppose the monsters are being made up";
		mes "inside the fortress.";
		mes "My guess is that if we can defeat their ringleader in the fortress,";
		mes "the monsters will go away";
		mes "and Prontera will become stabilized.";
		mes "***";
		next;
		mes "[Scientist Doyeon]";
		mes "So I wanted to enter the fortress";
		mes "but couldn't find any entrance.";
		mes "I studied day and night";
		mes "and finally located it.";
		next;
		mes "[Scientist Doyeon]";
		mes "To enter the fortress,";
		mes "the warp should be activated.";
		mes "To do so, a lot of magic";
		mes "is indispensable!";
		next;
		mes "[Scientist Doyeon]";
		mes "And then...I've been preparing";
		mes "a lot of magic necessary for the warp.";
		mes "Now I could use a warrior";
		mes "who can shatter the fortress";
		mes "and bring peace to Prontera.";
		mes "That's why I'm here.";
		close;
	case 2:
		mes "[Scientist Doyeon]";
		mes "To operate the warp,";
		mes "a lot of magic is required.";
		mes "Don't forget to bring me";
		mes "the -Dungeon Pass-.";
		mes "That's the only way to open";
		mes "the warp to the Sky Fortress.";
		next;
		if (select( "End the dialogue.", "Show the -Dungeon Pass-." ) == 1) {
			mes "[Scientist Doyeon]";
			mes "Don't forget to bring me";
			mes "the -Dungeon Pass-.";
			mes "Enter the Sky Fortress";
			mes "and help us save";
			mes "the invaded Prontera.";
			close;
		}
		if (countitem(14505) < 1 && countitem(14506) < 1) {// Sky Fortress Ticket
			mes "[Scientist Doyeon]";
			mes "I don't think this works";
			mes "without the -Dungeon Pass-.";
			mes "I can't open the warp without it.";
			close;
		}
		mes "[Scientist Doyeon]";
		mes "Oh! Where did you";
		mes "get the -Dungeon Pass-?";
		mes "This item is enough";
		mes "to open the warp.";
		next;
		if (select( "End the dialogue.", "Activate the warp." ) == 1) {
			mes "[Scientist Doyeon]";
			mes "Are you not going";
			mes "to help Prontera?";
			close;
		}
		mes "[Scientist Doyeon]";
		mes "There! I opened the warp";
		mes "to the Sky Fortress!";
		mes "The warp will be activated";
		mes "only for a limited period of time.";
		mes "Step inside it, quick!";
		instance_create("Sky Fortress Invasion");
		close;
	case 3:
		close;
	}
}

// Warps
1@sthb,73,71,0	warp2	#sthd_move_01_01	1,1,1@sthb,73,84
1@sthb,93,77,0	warp2	#sthd_move_02_01	2,2,1@sthb,210,96
1@sthb,190,54,0	warp2	#sthd_move_02_02	2,2,1@sthd,103,71

// Event 1 entrance
1@sthb,64,67,4	script	Stefan.J.E.Wolf#sthd_01	4_AS_RAGGED_GOLEM,{ end; }
1@sthb,64,67,4	script	Stefan.J.E.Wolf#sthd_02	4_AS_RAGGED_GOLEM,{ end; }

1@sthb,56,71,0	script	#sthd_event_1	HIDDEN_WARP_NPC,4,4,{
	end;
OnTouch:
	if ('sky_status == 0) {
		'sky_status = 1;
		.@stefan$ = instance_npcname("Stefan.J.E.Wolf#sthd_01");

		mes "";	// player attached
		unittalk getcharid(3), "" + strcharinfo(0) + " : Is this what caused the invasion into Prontera? Inside the Sky Fortress?";
		sleep2 4000;
		cutin "stephan_j_e_w.bmp",2;
		npctalk "Wolf: Trespassers... Sky Fortress...", .@stefan$;
		specialeffect2 EF_LOCKON;
		sleep2 2000;
		npctalk "Jack: Bijou...told me...to impede the intruders...", .@stefan$;
		sleep2 2000;
		unittalk getcharid(3), "" + strcharinfo(0) + " : What's that...huge lump?";
		sleep2 200;
		npctalk "Earnest: Little...human...Greenhorn...Away...", .@stefan$;
		sleep2 1800;
		cutin "",255;
		specialeffect2 EF_LOCKON;
		monster 'sthb_map$,64,67, "Stefan.J.E.Wolf",3484,1, instance_npcname("#sthd_event_2") + "::OnMobDead";	// AS_D_RAGGED_GOLEM
		'boss_id = $@mobid[0];
		donpcevent instance_npcname("#sthd_event_2") + "::OnStefanHp";
		disablenpc instance_npcname("#sthd_event_1");
		disablenpc .@stefan$;
	}
	end;
}

// 1@sthb,68,65,0	script	#sthd_event_2	HIDDEN_WARP_NPC,{	// official coord
1@sthb,1,1,0	script	#sthd_event_2	HIDDEN_WARP_NPC,{
	end;
OnStefanHp:
	enablenpc instance_npcname("#sthd_event_2");
	initnpctimer;
	end;
OnTimer1000:
	getunitdata 'boss_id, .@data;
	if ((.@data[UMOB_MAXHP] - .@data[UMOB_HP]) < (.@data[UMOB_MAXHP] / 10))	// lower than 90% hpmax ? 90% of 20M hp
		initnpctimer;
	else {
		stopnpctimer;
		killmonster 'sthb_map$, instance_npcname("#sthd_event_2") + "::OnMobDead";
		donpcevent instance_npcname("#sthd_event_3") + "::OnEvent";
		disablenpc instance_npcname("#sthd_event_2");
	}
	end;
OnMobDead:
	stopnpctimer;
	donpcevent instance_npcname("#sthd_event_3") + "::OnEvent";
	disablenpc instance_npcname("#sthd_event_2");
	end;
}

// 1@sthb,68,66,0	script	#sthd_event_3	HIDDEN_WARP_NPC,{	// official coord
1@sthb,1,1,0	script	#sthd_event_3	HIDDEN_WARP_NPC,{
	end;
OnEvent:
	enablenpc instance_npcname("#sthd_event_3");
	specialeffect EF_FLASHER;
	initnpctimer;
	end;
OnTimer1500:
	enablenpc instance_npcname("Stefan.J.E.Wolf#sthd_01");
	end;
OnTimer2500:
	npctalk "Stefan: ~Grunts~", instance_npcname("Stefan.J.E.Wolf#sthd_01");
	end;
OnTimer4000:
	npctalk "Earnest: What a bunch of menacing worms...!", instance_npcname("Stefan.J.E.Wolf#sthd_01");
	mapannounce 'sthb_map$, "Stefan Jack Earnest Wolf: Guard...the Sky Fortress...Immortal...beings...", bc_map,0xEBFF;
	donpcevent instance_npcname("#sthd_event_4") + "::OnStart";
	end;
OnTimer5000:
	disablenpc instance_npcname("#sthd_event_3");
	disablenpc instance_npcname("Stefan.J.E.Wolf#sthd_01");
	stopnpctimer;
	end;
}

// official npc, overlap #sthd_event_1 range : the killer directly trigger the next event for now
// 1@sthb,64,67,0	script	#Stefan_Action1	HIDDEN_WARP_NPC,8,8,{ end; }

// 1@sthb,68,67,0	script	#sthd_event_4	HIDDEN_WARP_NPC,{	// official coord
1@sthb,1,1,0	script	#sthd_event_4	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#sthd_event_4");
	callsub S_Spawn;
OnMobDead:
	if (mobcount( 'sthb_map$, instance_npcname("#sthd_event_4") + "::OnMobDead" ) > 0)
		end;
	'sky_status++;
	switch( 'sky_status ) {
	case 2:
		callsub S_Spawn;
	case 3:
		monster 'sthb_map$,56,79,"Sky Fortress Key Keeper",3478,1, instance_npcname("#sthd_event_4") + "::OnMobDead";	// AS_ZOMBIE_SLAUGHTER
		callsub S_Spawn;
	case 4:
		// enablenpc instance_npcname("#Stefan_Action1");
		enablenpc instance_npcname("Stefan.J.E.Wolf#sthd_02");
		disablenpc instance_npcname("#sthd_event_4");
		break;
	}
	// disablenpc instance_npcname("#Stefan_Action1");
	mes "";	// player attached
	sleep2 2000;
	cutin "stephan_j_e_w.bmp",2;
	unittalk getcharid(3), "" + strcharinfo(0) + " : I believe that huge Golem is what plays an important role in the Sky Fortress.";
	sleep2 2000;
	npctalk "Wolf: Every subordinate at the fortress...Intruders...Stop them...Bijou's...order...", instance_npcname("Stefan.J.E.Wolf#sthd_02");
	specialeffect2 EF_LOCKON;
	sleep2 2000;
	unittalk getcharid(3), "" + strcharinfo(0) + " : What on earth is happening inside?";
	sleep2 2000;
	unittalk getcharid(3), "" + strcharinfo(0) + " : Let's head to top! I believe there must be something there.";
	sleep2 2000;
	specialeffect2 EF_LOCKON;
	sleep2 1000;
	specialeffect EF_BAKU,AREA, instance_npcname("Stefan.J.E.Wolf#sthd_02");
	cutin "",255;
	disablenpc instance_npcname("Stefan.J.E.Wolf#sthd_02");
	for ( .@i = 1; .@i <= 6; .@i++ ) {
		enablenpc instance_npcname("Desactivated Warp#sthd_mov_" + .@i);
		enablenpc instance_npcname("#sthd_move_back_a_" + .@i);
	}
	enablenpc instance_npcname("#sthd_move_01_01");
	enablenpc instance_npcname("#sthd_move_02_01");
	enablenpc instance_npcname("#sthd_move_02_02");
	enablenpc instance_npcname("#chest_spawn");

	enablenpc instance_npcname("#Stefan_Action2");	// 2nd map
	enablenpc instance_npcname("Stefan.J.E.Wolf#sthd_03");

	// Mobs spots
	for ( .@i = 5; .@i <= 18; .@i++ )
		enablenpc instance_npcname("#sthd_event_" + .@i);

	// Room Shuffle
	donpcevent instance_npcname("#sthd_key_control") + "::OnShuffle";
	end;
S_Spawn:
	.@label$ = instance_npcname("#sthd_event_4") + "::OnMobDead";
	monster 'sthb_map$,48,75,"Immortal Zombie Soldier",3476,1, .@label$;// AS_ZOMBIE
	monster 'sthb_map$,56,75,"Immortal Zombie Soldier",3476,1, .@label$;
	monster 'sthb_map$,63,74,"Immortal Zombie Soldier",3476,1, .@label$;
	monster 'sthb_map$,63,67,"Immortal Zombie Soldier",3476,1, .@label$;
	monster 'sthb_map$,63,61,"Immortal Zombie Soldier",3476,1, .@label$;
	monster 'sthb_map$,56,60,"Immortal Zombie Soldier",3476,1, .@label$;
	monster 'sthb_map$,48,60,"Immortal Zombie Soldier",3476,1, .@label$;
	monster 'sthb_map$,48,67,"Immortal Zombie Soldier",3476,1, .@label$;
	end;
}

// Shuffle warp room
1@sthb,68,80,0	script	#sthd_key_control	HIDDEN_WARP_NPC,{
	end;
OnShuffle:
	setarray .@index[0],0,1,2,3,4,5;
	.@size = 6;

	for ( .@i = 0; .@i < 6; .@i++ ) {
		.@r = rand(.@size);
		'room_in_coord[.@i] = .@index[.@r] * 2;	// index warp in
		'room_back_coord[ .@index[.@r] ] = .@i * 2;	// index warp out
		.@index[.@r] = .@index[ .@size - 1 ];
		.@size--;
	}
	end;
}

// Warp outside
1@sthc,66,88,0	script	#sthd_move_back_a_1	WARPNPC,2,2,{
	end;
OnTouch:
	setarray .@coord[0],
		 39,85,	// Activated Warp#sthd_mov_1
		 83,95,	// Activated Warp#sthd_mov_2
		 28,39,	// Activated Warp#sthd_mov_3
		210,79,	// Activated Warp#sthd_mov_4
		143,87,	// Activated Warp#sthd_mov_5
		179,47;	// Activated Warp#sthd_mov_6
	.@room_num = atoi( replacestr( strnpcinfo(2), "sthd_move_back_a_", "" ) ) - 1;
	warp 'sthb_map$, .@coord[ 'room_back_coord[.@room_num] ], .@coord[ 'room_back_coord[.@room_num]+1 ];
	end;
}

1@sthc,116,88,0	duplicate(#sthd_move_back_a_1)	#sthd_move_back_a_2	WARPNPC,2,2
1@sthc,16,6,0	duplicate(#sthd_move_back_a_1)	#sthd_move_back_a_3	WARPNPC,2,2
1@sthc,115,6,0	duplicate(#sthd_move_back_a_1)	#sthd_move_back_a_4	WARPNPC,2,2
1@sthc,66,6,0	duplicate(#sthd_move_back_a_1)	#sthd_move_back_a_5	WARPNPC,2,2
1@sthc,15,88,0	duplicate(#sthd_move_back_a_1)	#sthd_move_back_a_6	WARPNPC,2,2

// Warp entrance
1@sthb,34,86,1	script	Activated Warp#sthd_mov_1	1_SHADOW_NPC,{
	if (select( "Don't step into the warp.", "Step into the warp." ) == 1) {
		mes "- Dreary warp -";
		mes "- Black light is shining. -";
		close;
	}
	mes "- Black Warp is swirling about. -";
	close2;
	setarray .@coord[0],
		 66,96,	// #sthd_move_back_a_1, #sthd_event_13
		116,96,	// #sthd_move_back_a_2, #sthd_event_14
		 16,13,	// #sthd_move_back_a_3, #sthd_event_18, Immortal Wind Ghost
		115,14,	// #sthd_move_back_a_4, #sthd_event_15
		 66,14,	// #sthd_move_back_a_5, #sthd_event_16
		 15,96;	// #sthd_move_back_a_6, #sthd_event_17, Immortal Cursed Knight

	.@room_num = atoi( replacestr( strnpcinfo(2), "sthd_mov_", "" ) ) - 1;
	warp 'sthc_map$, .@coord[ 'room_in_coord[.@room_num] ], .@coord[ 'room_in_coord[.@room_num] + 1 ];
	end;
}

1@sthb,84,99,1	duplicate(Activated Warp#sthd_mov_1)	Activated Warp#sthd_mov_2	1_SHADOW_NPC
1@sthb,24,40,1	duplicate(Activated Warp#sthd_mov_1)	Activated Warp#sthd_mov_3	1_SHADOW_NPC
1@sthb,206,80,1	duplicate(Activated Warp#sthd_mov_1)	Activated Warp#sthd_mov_4	1_SHADOW_NPC
1@sthb,147,86,1	duplicate(Activated Warp#sthd_mov_1)	Activated Warp#sthd_mov_5	1_SHADOW_NPC
1@sthb,179,51,1	duplicate(Activated Warp#sthd_mov_1)	Activated Warp#sthd_mov_6	1_SHADOW_NPC

1@sthb,34,85,1	script	Desactivated Warp#sthd_mov_1	4_ENERGY_WHITE,{
	if (select( "Desactivate the warp.", "Activate the warp." ) == 1) {
		mes "- Move the fortress to a special place -";
		mes "- Accessible door -";
		mes "- You seen an accessible door -";
		close;
	}
	if (countitem(6960) < 1) {
		mes "- To operate the warp -";
		mes "- You need a key to the Sky Fortress. -";
		close;
	}
	delitem 6960,1;
	mes "- Warp has been -";
	mes "- successfully activated -";
	mes "- Dreary warp -";
	mes "- is brought up -";
	mes "- shining in black -";
	disablenpc instance_npcname( strnpcinfo(0) );
	enablenpc instance_npcname("Activated Warp#" + strnpcinfo(2));
	close;
}

1@sthb,83,99,1	duplicate(Desactivated Warp#sthd_mov_1)	Desactivated Warp#sthd_mov_2	4_ENERGY_WHITE
1@sthb,24,39,1	duplicate(Desactivated Warp#sthd_mov_1)	Desactivated Warp#sthd_mov_3	4_ENERGY_WHITE
1@sthb,206,79,1	duplicate(Desactivated Warp#sthd_mov_1)	Desactivated Warp#sthd_mov_4	4_ENERGY_WHITE
1@sthb,147,87,1	duplicate(Desactivated Warp#sthd_mov_1)	Desactivated Warp#sthd_mov_5	4_ENERGY_WHITE
1@sthb,178,51,1	duplicate(Desactivated Warp#sthd_mov_1)	Desactivated Warp#sthd_mov_6	4_ENERGY_WHITE

// Rooms
1@sthc,66,94,0	script	#sthd_event_13	HIDDEN_WARP_NPC,2,2,{
	end;
OnTouch:
	.@label$ = instance_npcname("#chest_spawn") + "::OnTreasure1";
	monster 'sthc_map$,62,108,"Immortal Nightmare Shadow",3481,1, .@label$;// AS_EVIL_SHADOW1
	monster 'sthc_map$,70,108,"Immortal Nightmare Shadow",3481,1, .@label$;
	monster 'sthc_map$,70,101,"Immortal Nightmare Shadow",3481,1, .@label$;
	monster 'sthc_map$,66,115,"Immortal Nightmare Shadow",3481,1, .@label$;
	monster 'sthc_map$,70,116,"Immortal Nightmare Shadow",3481,1, .@label$;
	monster 'sthc_map$,69,127,"Immortal Nightmare Shadow",3481,1, .@label$;
	monster 'sthc_map$,64,132,"Immortal Nightmare Shadow",3481,1, .@label$;
	monster 'sthc_map$,61,127,"Immortal Nightmare Shadow",3481,1, .@label$;
	monster 'sthc_map$,62,116,"Immortal Nightmare Shadow",3481,1, .@label$;
	disablenpc instance_npcname("#sthd_event_13");
	end;
}

1@sthc,116,94,0	script	#sthd_event_14	HIDDEN_WARP_NPC,2,2,{
	end;
OnTouch:
	.@label$ = instance_npcname("#chest_spawn") + "::OnTreasure2";
	monster 'sthc_map$,112,101, "Immortal Angry Shadow#1",3482,1, .@label$;// AS_EVIL_SHADOW2
	monster 'sthc_map$,120,101, "Immortal Angry Shadow#2",3482,1, .@label$;
	monster 'sthc_map$,119,110, "Immortal Angry Shadow#4",3482,1, .@label$;
	monster 'sthc_map$,115,115, "Immortal Angry Shadow#1",3482,1, .@label$;
	monster 'sthc_map$,114,128, "Immortal Angry Shadow#9",3482,1, .@label$;
	monster 'sthc_map$,112,110, "Immortal Angry Shadow#3",3482,1, .@label$;
	monster 'sthc_map$,118,120, "Immortal Angry Shadow#6",3482,1, .@label$;
	monster 'sthc_map$,110,130, "Immortal Angry Shadow#7",3482,1, .@label$;
	monster 'sthc_map$,114,122, "Immortal Angry Shadow#5",3482,1, .@label$;
	monster 'sthc_map$,116,125, "Immortal Angry Shadow#8",3482,1, .@label$;
	disablenpc instance_npcname("#sthd_event_14");
	end;
}

1@sthc,115,12,0	script	#sthd_event_15	HIDDEN_WARP_NPC,2,2,{
	end;
OnTouch:
	.@label$ = instance_npcname("#chest_spawn") + "::OnTreasure3";
	monster 'sthc_map$,120,27, "Immortal Nightmare Shadow",3481,1, .@label$;// AS_EVIL_SHADOW1
	monster 'sthc_map$,120,19, "Immortal Nightmare Shadow",3481,1, .@label$;
	monster 'sthc_map$,111,27, "Immortal Nightmare Shadow",3481,1, .@label$;
	monster 'sthc_map$,115,33, "Immortal Nightmare Shadow",3481,1, .@label$;
	monster 'sthc_map$,111,36, "Immortal Nightmare Shadow",3481,1, .@label$;
	monster 'sthc_map$,111,45, "Immortal Nightmare Shadow",3481,1, .@label$;
	monster 'sthc_map$,114,49, "Immortal Nightmare Shadow",3481,1, .@label$;
	monster 'sthc_map$,119,36, "Immortal Nightmare Shadow",3481,1, .@label$;
	monster 'sthc_map$,119,45, "Immortal Nightmare Shadow",3481,1, .@label$;
	monster 'sthc_map$,116,18, "Immortal Nightmare Shadow",3481,1, .@label$;
	disablenpc instance_npcname("#sthd_event_15");
	end;
}

1@sthc,66,12,0	script	#sthd_event_16	HIDDEN_WARP_NPC,2,2,{
	end;
OnTouch:
	.@label$ = instance_npcname("#chest_spawn") + "::OnTreasure4";
	monster 'sthc_map$,70,18, "Immortal Angry Shadow#2",3482,1, .@label$;// AS_EVIL_SHADOW2
	monster 'sthc_map$,61,18, "Immortal Angry Shadow#1",3482,1, .@label$;
	monster 'sthc_map$,65,34, "Immortal Angry Shadow#1",3482,1, .@label$;
	monster 'sthc_map$,62,38, "Immortal Angry Shadow#5",3482,1, .@label$;
	monster 'sthc_map$,69,37, "Immortal Angry Shadow#6",3482,1, .@label$;
	monster 'sthc_map$,72,29, "Immortal Angry Shadow#4",3482,1, .@label$;
	monster 'sthc_map$,69,47, "Immortal Angry Shadow#8",3482,1, .@label$;
	monster 'sthc_map$,62,47, "Immortal Angry Shadow#7",3482,1, .@label$;
	monster 'sthc_map$,61,30, "Immortal Angry Shadow#3",3482,1, .@label$;
	monster 'sthc_map$,64,50, "Immortal Angry Shadow#9",3482,1, .@label$;
	disablenpc instance_npcname("#sthd_event_16");
	end;
}

1@sthc,15,94,0	script	#sthd_event_17	HIDDEN_WARP_NPC,2,2,{
	end;
OnTouch:
	.@label$ = instance_npcname("#chest_spawn") + "::OnTreasureB";
	monster 'sthc_map$,19,106, "Immortal Death Shadow#2",3483,1, .@label$;// AS_EVIL_SHADOW3
	monster 'sthc_map$,12,105, "Immortal Death Shadow#1",3483,1, .@label$;
	monster 'sthc_map$,14,115, "Immortal Death Shadow#3",3483,1, .@label$;
	monster 'sthc_map$,19,119, "Immortal Death Shadow#4",3483,1, .@label$;
	monster 'sthc_map$,13,116, "Immortal Death Shadow#3",3483,1, .@label$;
	monster 'sthc_map$,18,127, "Immortal Death Shadow#6",3483,1, .@label$;
	monster 'sthc_map$,19,128, "Immortal Death Shadow#6",3483,1, .@label$;
	monster 'sthc_map$,12,126, "Immortal Death Shadow#5",3483,1, .@label$;
	monster 'sthc_map$,20,119, "Immortal Death Shadow#4",3483,1, .@label$;
	monster 'sthc_map$,11,127, "Immortal Death Shadow#5",3483,1, .@label$;
	disablenpc instance_npcname("#sthd_event_17");
	end;
}

1@sthc,16,11,0	script	#sthd_event_18	HIDDEN_WARP_NPC,2,2,{
	end;
OnTouch:
	.@label$ = instance_npcname("#chest_spawn") + "::OnWindGhostRoom";
	monster 'sthc_map$,19,21, "Immortal Death Shadow#2",3483,1, .@label$;// AS_EVIL_SHADOW3
	monster 'sthc_map$,12,21, "Immortal Death Shadow#1",3483,1, .@label$;
	monster 'sthc_map$,11,30, "Immortal Death Shadow#3",3483,1, .@label$;
	monster 'sthc_map$,10,31, "Immortal Death Shadow#3",3483,1, .@label$;
	monster 'sthc_map$,16,38, "Immortal Death Shadow#5",3483,1, .@label$;
	monster 'sthc_map$,15,38, "Immortal Death Shadow#5",3483,1, .@label$;
	monster 'sthc_map$,15,45, "Immortal Death Shadow#6",3483,1, .@label$;
	monster 'sthc_map$,14,45, "Immortal Death Shadow#6",3483,1, .@label$;
	monster 'sthc_map$,19,31, "Immortal Death Shadow#4",3483,1, .@label$;
	monster 'sthc_map$,19,32, "Immortal Death Shadow#4",3483,1, .@label$;
	disablenpc instance_npcname("#sthd_event_18");
	end;
}

1@sthc,1,1,0	script	#chest_spawn	HIDDEN_WARP_NPC,{
	end;
OnTreasure1: callsub( S_Treasure,1 );
OnTreasure2: callsub( S_Treasure,2 );
OnTreasure3: callsub( S_Treasure,3 );
OnTreasure4: callsub( S_Treasure,4 );
S_Treasure:
	.@num = getarg(0);
	if (mobcount( 'sthc_map$, instance_npcname("#chest_spawn") + "::OnTreasure" + .@num ) < 1)
		enablenpc instance_npcname("Sky Fortress Gold Treasure#" + .@num);
	end;
OnTreasureB:
	if (mobcount( 'sthc_map$, instance_npcname("#chest_spawn") + "::OnTreasureB" ) < 1)
		enablenpc instance_npcname("Sky Fortress Treasure B");
	end;
OnKnightB:
	if (mobcount( 'sthc_map$, instance_npcname("#chest_spawn") + "::OnKnightB" ) < 1)
		mapannounce 'sthc_map$, "Stefan Jack Earnest Wolf: Fell down... Immortal Cursed Knight... Sad... Will kill... The intruder...", bc_map,0xEBFF;
	end;
OnWindGhostRoom:
	if (mobcount( 'sthc_map$, instance_npcname("#chest_spawn") + "::OnWindGhostRoom" ) < 1)
		enablenpc instance_npcname("Wind Ghost in Experiment");
	end;
OnWindDead:
	if (mobcount( 'sthc_map$, instance_npcname("#chest_spawn") + "::OnWindDead" ) < 1)
		mapannounce 'sthc_map$, "Stefan Jack Earnest Wolf: Experiment... Became stronger... Immortal Wind Ghost...is dead...Death to all...", bc_map,0xEBFF;
	end;
}

// Chest
1@sthc,66,137,4	script	Sky Fortress Gold Treasure#1	4_TREASURE_BOX,{
	mes "- You've found -";
	mes "- the Shining Treasure Box. -";
	next;
	if (select( "Open the box.", "Do not open the box." ) == 2) {
		mes "- If you open the box, -";
		mes "- there must be some good items. -";
		close;
	}
	if (getpartyleader(getcharid(1),2) != getcharid(0)) {
		mes "- Those other than the party leader -";
		mes "- don't seem to be able to open the box. -";
		close;
	}
	donpcevent instance_npcname( strnpcinfo(0) ) + "::OnBar";
	sleep2 3000;
	specialeffect EF_ENHANCE;
	mes "- In that treasure box, -";
	if (rand(100) < 50) {// Inaccurate / maybe more rewards
		getitem 985,1;
		mes "- you've found the Elunium. -";
	}
	else {
		getitem 984,1;
		mes "- you've found the Oridecon. -";
	}
	disablenpc instance_npcname( strnpcinfo(0) );
	close;
OnBar:
	progressbar_npc "000000",3;
	end;
}

1@sthc,116,137,4	duplicate(Sky Fortress Gold Treasure#1)	Sky Fortress Gold Treasure#2	4_TREASURE_BOX
1@sthc,116,55,4	duplicate(Sky Fortress Gold Treasure#1)	Sky Fortress Gold Treasure#3	4_TREASURE_BOX
1@sthc,66,55,4	duplicate(Sky Fortress Gold Treasure#1)	Sky Fortress Gold Treasure#4	4_TREASURE_BOX

// Cursed Knight
1@sthc,16,132,4	script	Sky Fortress Treasure B	CLEAR_NPC,{
	mes "- You've found -";
	mes "- the Shining Treasure Box. -";
	next;
	if (select( "Open the box.", "Do not open the box." ) == 2) {
		mes "- If you open the box, -";
		mes "- there must be some good items. -";
		close;
	}
	if (getpartyleader(getcharid(1),2) != getcharid(0)) {
		mes "- Those other than the party leader -";
		mes "- don't seem to be able to open the box. -";
		close;
	}
	mes "- The treasure box is opening up -";
	mes "- and something suddenly -";
	mes "- showed up in the front. -";
	specialeffect EF_SILENT_BREEZE;
	specialeffect EF_LOCKON;
	unittalk getcharid(3), "" + strcharinfo(0) + " : You may have touched something wrong...";
	disablenpc instance_npcname("Sky Fortress Treasure B");
	donpcevent instance_npcname("Immortal Cursed Knight#") + "::OnEvent";
	close;
}

1@sthc,16,129,4	script	Immortal Cursed Knight#	4_AS_BLOODY_KNIGHT,{
	end;
OnEvent:
	enablenpc instance_npcname("Immortal Cursed Knight#");
	initnpctimer;
	end;
OnTimer2000:
	npctalk "Immortal Cursed Knight: What kind of intruder is trying to wake me up...";
	end;
OnTimer4000:
	npctalk "Immortal Cursed Knight: Doom to the petty thief coveting the treasure at the fortress...";
	end;
OnTimer6000:
	disablenpc instance_npcname("Immortal Cursed Knight#");
	monster 'sthc_map$,16,129,"Immortal Cursed Knight",3474,1, instance_npcname("#chest_spawn") + "::OnKnightB";	// AS_BLOODY_KNIGHT
	stopnpctimer;
	end;
}

// Wind Ghost
1@sthc,16,54,4	script	Wind Ghost in Experiment	CLEAR_NPC,{
	if (select( "Examine the test tube.", "Do not examine the test tube." ) == 2) {
		mes "- I'm sure there is something -";
		mes "- extremely fearsome. -";
		close;
	}
	if (getpartyleader(getcharid(1),2) != getcharid(0)) {
		mes "- Those other than the party leader -";
		mes "- don't seem to be able operate the test tube. -";
		close;
	}
	mes "- The test tube is opening up -";
	mes "- and something suddenly -";
	mes "- showed up in the front. -";
	specialeffect EF_SILENT_BREEZE;
	specialeffect EF_LOCKON;
	unittalk getcharid(3), "" + strcharinfo(0) + " : What...what is this?";
	donpcevent instance_npcname("Immortal Wind Ghost#01") + "::OnEvent";
	disablenpc instance_npcname("Wind Ghost in Experiment");
	close;
}

1@sthc,15,49,4	script	Immortal Wind Ghost#01	4_AS_WIND_GHOST,{
	end;
OnEvent:
	enablenpc instance_npcname("Immortal Wind Ghost#01");
	initnpctimer;
	end;
OnTimer2000:
	npctalk "Immortal Wind Ghost: Foolish human...You're praying for death...";
	end;
OnTimer4000:
	npctalk "Immortal Wind Ghost: The power boiling inside my body...It sure feels good...";
	end;
OnTimer6000:
	stopnpctimer;
	monster 'sthc_map$,15,49,"Immortal Wind Ghost",3475,1, instance_npcname("#chest_spawn") + "::OnWindDead";	// AS_WIND_GHOST
	disablenpc instance_npcname("Immortal Wind Ghost#01");
	end;
}

// Spawn Step 1
1@sthb,65,86,0	script	#sthd_event_5	HIDDEN_WARP_NPC,5,5,{
	end;
OnTouch:
	disablenpc instance_npcname("#sthd_event_5");
	monster 'sthb_map$,61,84,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,51,87,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,37,81,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,67,50,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,41,85,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,41,49,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,52,50,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,36,60,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,36,58,"Immortal Cursed Zombie",3480,1;
	if (rand(100) < 10)	// unknown rates
		monster 'sthb_map$,65,86,"Sky Fortress Key Keeper",3478,1;	// unknown coord
	end;
}
	
1@sthb,62,49,0	script	#sthd_event_6	HIDDEN_WARP_NPC,5,5,{
	end;
OnTouch:
	disablenpc instance_npcname("#sthd_event_6");
	monster 'sthb_map$,79,48,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,85,60,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,82,74,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,83,49,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,83,71,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,79,96,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,69,95,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,82,50,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,84,85,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,84,95,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,83,94,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,84,86,"Immortal Cursed Zombie",3480,1;
	if (rand(100) < 10)	// unknown rates
		monster 'sthb_map$,80,49,"Sky Fortress Key Keeper",3478,1;
	end;
}

1@sthb,57,96,0	script	#sthd_event_7	HIDDEN_WARP_NPC,5,5,{
	end;
OnTouch:
	disablenpc instance_npcname("#sthd_event_7");
	monster 'sthb_map$,52,94,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,42,98,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,29,91,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,28,96,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,25,78,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,43,41,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,27,69,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,26,55,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,32,41,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,29,68,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,29,67,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,27,42,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,26,43,"Immortal Cursed Zombie",3480,1;
	if (rand(100) < 10)	// unknown rates
		monster 'sthb_map$,57,96,"Sky Fortress Key Keeper",3478,1;	// unknown coord
	end;
}

1@sthb,32,39,0	script	#sthd_event_8	HIDDEN_WARP_NPC,5,5,{
	end;
OnTouch:
	disablenpc instance_npcname("#sthd_event_8");
	monster 'sthb_map$,52,43,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,64,41,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,78,38,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,28,40,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,59,40,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,92,45,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,95,57,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,90,42,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,90,40,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,93,39,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,94,39,"Immortal Cursed Zombie",3480,1;
	if (rand(100) < 10)	// unknown rates
		monster 'sthb_map$,32,39,"Sky Fortress Key Keeper",3478,1;	// unknown coord
	end;
}

// Spawn Step 2
1@sthb,209,64,0	script	#sthd_event_9	HIDDEN_WARP_NPC,5,5,{
	end;
OnTouch:
	disablenpc instance_npcname("#sthd_event_9");
	monster 'sthb_map$,207,50,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,205,38,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,195,36,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,209,38,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,161,39,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,150,36,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,171,37,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,171,36,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,144,38,"Immortal Cursed Zombie",3480,1;
	if (rand(100) < 10)	// unknown rates
		monster 'sthb_map$,163,38,"Sky Fortress Key Keeper",3478,1;
	end;
}

1@sthb,144,47,0	script	#sthd_event_10	HIDDEN_WARP_NPC,5,5,{
	end;
OnTouch:
	disablenpc instance_npcname("#sthd_event_10");
	monster 'sthb_map$,142,61,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,145,71,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,142,83,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,147,94,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,143,64,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,144,78,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,165,95,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,173,92,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,156,91,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,144,93,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,143,94,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,155,92,"Immortal Cursed Zombie",3480,1;
	if (rand(100) < 10)	// unknown rates
		monster 'sthb_map$,144,47,"Sky Fortress Key Keeper",3478,1;	// unknown coord
	end;
}

1@sthb,178,94,0	script	#sthd_event_11	HIDDEN_WARP_NPC,5,5,{
	end;
OnTouch:
	disablenpc instance_npcname("#sthd_event_11");
	monster 'sthb_map$,187,92,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,200,90,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,198,81,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,201,72,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,199,94,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,199,70,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,195,48,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,185,47,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,197,61,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,198,62,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,200,46,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,199,48,"Immortal Cursed Zombie",3480,1;
	if (rand(100) < 10)	// unknown rates
		monster 'sthb_map$,178,94,"Sky Fortress Key Keeper",3478,1;	// unknown coord
	end;
}

1@sthb,179,48,0	script	#sthd_event_12	HIDDEN_WARP_NPC,5,5,{
	end;
OnTouch:
	disablenpc instance_npcname("#sthd_event_12");
	monster 'sthb_map$,167,44,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,159,46,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,154,52,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,152,65,"Immortal Fortress Legio",3477,1;
	monster 'sthb_map$,155,48,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,154,73,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,159,84,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,171,82,"Immortal Zombie Assault",3479,1;
	monster 'sthb_map$,153,84,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,155,78,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,155,76,"Immortal Cursed Zombie",3480,1;
	monster 'sthb_map$,153,84,"Immortal Cursed Zombie",3480,1;
	if (rand(100) < 10)	// unknown rates
		monster 'sthb_map$,179,48,"Sky Fortress Key Keeper",3478,1;	// unknown coord
	end;
}

// 2nd step
1@sthb,207,83,0	script	#Stefan_Action2	HIDDEN_WARP_NPC,6,6,{
	end;
OnTouch:
	disablenpc instance_npcname("#Stefan_Action2");
	mes "";	// player attached
	sleep2 2000;
	cutin "stephan_j_e_w.bmp",2;
	npctalk "Stefan: ~Grunts~", instance_npcname("Stefan.J.E.Wolf#sthd_03");
	sleep2 2000;
	npctalk "Earnest: Filthy humans...Stop them...Guard...the holy fortress...", instance_npcname("Stefan.J.E.Wolf#sthd_03");
	specialeffect2 EF_LOCKON;
	sleep2 2000;
	unittalk getcharid(3), "" + strcharinfo(0) + " : It's extremely vigilant of me.";
	sleep2 2000;
	unittalk getcharid(3), "" + strcharinfo(0) + " : I think there must be something if we keep following";
	sleep2 2000;
	unittalk getcharid(3), "" + strcharinfo(0) + " : that Golem's trace!";
	sleep2 2000;
	mapannounce 'sthb_map$, "Stefan Jack Earnest Wolf: Holy fortress...Intruders...How wicked...Stop them...", bc_map,0xEBFF;
	cutin "",255;
	sleep2 2000;
	specialeffect2 EF_LOCKON;
	disablenpc instance_npcname("Stefan.J.E.Wolf#sthd_03");
	enablenpc instance_npcname("#Stefan for display3");
	enablenpc instance_npcname("Stefan.J.E.Wolf#sthd_04");
	end;
}

1@sthb,207,83,8	script	Stefan.J.E.Wolf#sthd_03	4_AS_RAGGED_GOLEM,{ end; }

// Final step
1@sthd,103,115,6	script	Stefan.J.E.Wolf#sthd_04	4_AS_RAGGED_GOLEM,{ end; }

1@sthd,103,115,0	script	#Stefan for display3	HIDDEN_WARP_NPC,10,10,{
	end;
OnTouch:
	.@stephan$ = instance_npcname("Stefan.J.E.Wolf#sthd_04");
	disablenpc instance_npcname("#Stefan for display3");
	mes "";	// fix me
	unittalk getcharid(3), "" + strcharinfo(0) + " : So this is finally the last...I suppose he must be the cause of Prontera Invasion, isn't he?";
	sleep2 2000;
	cutin "stephan_j_e_w.bmp",2;
	npctalk "Wolf: Adventurers...A bunch of foolish greenhorns...", .@stephan$;
	sleep2 2000;
	npctalk "Jack: I must follow...order...from Bijou...", .@stephan$;
	sleep2 2000;
	unittalk getcharid(3), "" + strcharinfo(0) + " : Bijou? So, was Golem the mere guardian around here? Is that it...?";
	sleep2 2000;
	npctalk "Stefan: ~Growls~", .@stephan$;
	sleep2 2000;
	npctalk "Earnest: Filthy worms...Can't lead life as intended...Immortal power...My body...", .@stephan$;
	sleep2 2000;
	unittalk getcharid(3), "" + strcharinfo(0) + " : I'd better defeat it first.";
	sleep2 2000;
	mapannounce 'sthd_map$, "Stefan Jack Earnest Wolf: Soldiers...Bijou's...fortress...Cut them all...I'll step up...I'll kill them...all...", bc_map,0xEBFF;
	sleep2 2000;
	cutin "",255;
	specialeffect EF_LOCKON;
	disablenpc .@stephan$;
	enablenpc instance_npcname("#stefan_boss_event");
	monster 'sthd_map$,103,115,"Stefan.J.E.Wolf",3473,1, instance_npcname("#stefan_boss_event") + "::OnBossDead";		// AS_RAGGED_GOLEM
	'boss_id = $@mobid[0];
	donpcevent instance_npcname("#stefan_boss_status") + "::OnStart";
	donpcevent instance_npcname("#stefan_boss_zombie") + "::OnStart";
	initnpctimer;
	end;
OnTimer3000:
	donpcevent instance_npcname("#stefan_boss_talk") + "::OnTalk";
	end;
OnTimer30000:
	stopnpctimer;
	donpcevent instance_npcname("#stefan_boss_cursed") + "::OnStart";
	end;
OnStop:
	stopnpctimer;
	end;
}

1@sthd,1,1,0	script	#stefan_boss_zombie	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#stefan_boss_zombie");
	monster 'sthd_map$,0,0,"Zombie Soldier of Bijou",3476,10, instance_npcname("#stefan_boss_zombie") + "::OnMobDead";	// AS_ZOMBIE
	end;
OnMobDead:
	initnpctimer;
	end;
OnTimer1000:
	stopnpctimer;
	.@count = mobcount( 'sthd_map$, instance_npcname("#stefan_boss_zombie") + "::OnMobDead" );
	if (.@count <= 7)
		monster 'sthd_map$,0,0,"Zombie Soldier of Bijou",3476,(10-.@count), instance_npcname("#stefan_boss_zombie") + "::OnMobDead";	// AS_ZOMBIE
	end;
OnStop:
	stopnpctimer;
	disablenpc instance_npcname("#stefan_boss_zombie");
	killmonster 'sthd_map$, instance_npcname("#stefan_boss_zombie") + "::OnMobDead";
	end;
}

1@sthd,1,1,0	script	#stefan_boss_cursed	HIDDEN_WARP_NPC,{
	end;
OnStart:
	mapannounce 'sthd_map$, "Stefan Jack Earnest Wolf: Cursed soldiers...Come...Bring it on...", bc_map,0xEBFF;
	monster 'sthd_map$,0,0,"Cursed Soldier of Bijou",3485,5, instance_npcname("#stefan_boss_cursed") + "::OnMobDead";	// AS_D_CURSED_SOLDIER
	end;
OnStart2:
	killmonster 'sthd_map$, instance_npcname("#stefan_boss_cursed") + "::OnMobDead";
	monster 'sthd_map$,0,0,"Cursed Soldier of Bijou",3485,10, instance_npcname("#stefan_boss_cursed") + "::OnMobDead";	// AS_D_CURSED_SOLDIER
	end;
OnMobDead:
	end;
OnStop:
	disablenpc instance_npcname("#stefan_boss_cursed");
	killmonster 'sthd_map$, instance_npcname("#stefan_boss_cursed") + "::OnMobDead";
	end;
}

1@sthd,1,1,0	script	#stefan_boss_status	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#stefan_boss_status");
	initnpctimer;
	end;
OnTimer5000:	// ~5s - some announce can be skipped
	getunitdata 'boss_id, .@data;
	.@hp = .@data[UMOB_HP];

	if (.@hp > 6000000 && .@hp < 13000000 && 'status_event == 0) {// announce non-spammed
		'status_event = 1;
		mapannounce 'sthd_map$, "Stefan Jack Earnest Wolf: Strong...Adventurer... ~Growls~ My... soliders... Get out...", bc_map,0xEBFF;
	}
	else if (.@hp > 5000000 && .@hp <= 6000000) {// announce spammed
		'status_event = 0;
		mapannounce 'sthd_map$, "Stefan Jack Earnest Wolf: I'll destroy you...I'll be smashing you...Come, the Cursed Soldiers...Ground-shaking...", bc_map,0xEBFF;
	}
	else if (.@hp <= 5000000) {
		'status_event = 0;
		donpcevent instance_npcname("#stefan_boss_zombie") + "::OnStop";
		donpcevent instance_npcname("#stefan_boss_status") + "::OnStop";
		donpcevent instance_npcname("#stefan_boss_cursed") + "::OnStart2";
		donpcevent instance_npcname("#pantheon_damage_main") + "::OnStart";
		end;
	}
	initnpctimer;
	end;
OnStop:
	stopnpctimer;
	disablenpc instance_npcname("#stefan_boss_status");
	end;
}

1@sthd,1,1,0	script	#pantheon_damage_main	HIDDEN_WARP_NPC,{
	end;
OnStart:
	for ( .@i = 0; .@i < 25; .@i++ )
		'npc_index[.@i] = .@i + 1;
	donpcevent instance_npcname("#pantheon_damage_main") + "::OnEvent";
	end;
OnEvent:
	if ('boss_id < 1 || unitexists('boss_id) == false) {
		stopnpctimer;
		end;
	}
	getunitdata 'boss_id, .@data;
	if (.@data[UMOB_HP] > 5000000) {
		stopnpctimer;
		donpcevent instance_npcname("#stefan_boss_status") + "::OnStart";
		end;
	}
	for ( .@i = 0; .@i < 5; .@i++ ) {// 5 earthquakes
		.@size = 25 - .@i;
		.@r = rand(.@size);
		.@npc_num = 'npc_index[.@r];
		donpcevent instance_npcname(.@npc_num + "#pantheon_damage") + "::OnEvent";
		'npc_index[.@r] = 'npc_index[ .@size-1 ];
		'npc_index[ .@size-1 ] = .@npc_num;
	}
	initnpctimer;
	end;
OnTimer6000:
	donpcevent instance_npcname("#pantheon_damage_main") + "::OnEvent";
	end;
}

// Simulate earthquake on random spot 
1@sthd,94,119,0	script	1#pantheon_damage	HIDDEN_WARP_NPC,{
	end;
OnEvent:
	specialeffect EF_GUMGANG4;
	initnpctimer;
	// progressbar_npc "000000",3;	// officially non-visible, replaced by initnpctimer
	end;
OnTimer3000:	// note: the timer continue and end after boss dead
	stopnpctimer;
	specialeffect EF_NPC_EARTHQUAKE;
	donpcevent instance_npcname("#pantheon_damage" + strnpcinfo(1)) + "::OnEvent";
	end;
}
1@sthd,96,109,0	duplicate(1#pantheon_damage)	2#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,104,110,0	duplicate(1#pantheon_damage)	3#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,110,110,0	duplicate(1#pantheon_damage)	4#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,113,117,0	duplicate(1#pantheon_damage)	5#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,112,103,0	duplicate(1#pantheon_damage)	6#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,104,103,0	duplicate(1#pantheon_damage)	7#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,97,103,0	duplicate(1#pantheon_damage)	8#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,92,97,0	duplicate(1#pantheon_damage)	9#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,99,95,0	duplicate(1#pantheon_damage)	10#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,108,95,0	duplicate(1#pantheon_damage)	11#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,116,95,0	duplicate(1#pantheon_damage)	12#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,112,87,0	duplicate(1#pantheon_damage)	13#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,104,88,0	duplicate(1#pantheon_damage)	14#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,95,87,0	duplicate(1#pantheon_damage)	15#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,96,78,0	duplicate(1#pantheon_damage)	16#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,94,72,0	duplicate(1#pantheon_damage)	17#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,104,81,0	duplicate(1#pantheon_damage)	18#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,111,81,0	duplicate(1#pantheon_damage)	19#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,114,72,0	duplicate(1#pantheon_damage)	20#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,100,71,0	duplicate(1#pantheon_damage)	21#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,91,101,0	duplicate(1#pantheon_damage)	22#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,117,101,0	duplicate(1#pantheon_damage)	23#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,117,90,0	duplicate(1#pantheon_damage)	24#pantheon_damage	HIDDEN_WARP_NPC
1@sthd,90,90,0	duplicate(1#pantheon_damage)	25#pantheon_damage	HIDDEN_WARP_NPC
// official
// 1@sthd,100,71,0	duplicate(1#pantheon_damage)	23#pantheon_damage	HIDDEN_WARP_NPC
// 1@sthd,91,101,0	duplicate(1#pantheon_damage)	24#pantheon_damage	HIDDEN_WARP_NPC
// 1@sthd,117,101,0	duplicate(1#pantheon_damage)	25#pantheon_damage	HIDDEN_WARP_NPC
// 1@sthd,117,90,0	duplicate(1#pantheon_damage)	26#pantheon_damage	HIDDEN_WARP_NPC
// 1@sthd,90,90,0	duplicate(1#pantheon_damage)	27#pantheon_damage	HIDDEN_WARP_NPC

// Kill the player
1@sthd,94,119,0	script	#pantheon_damage1	HIDDEN_WARP_NPC,4,4,{
	end;
OnTouch:
	unitkill getcharid(3);
	end;
OnEvent:
	enablenpc instance_npcname( strnpcinfo(0) );
	initnpctimer;
	end;
OnTimer1000:
	stopnpctimer;
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}
1@sthd,96,109,0	duplicate(#pantheon_damage1)	#pantheon_damage2	HIDDEN_WARP_NPC,4,4
1@sthd,104,110,0	duplicate(#pantheon_damage1)	#pantheon_damage3	HIDDEN_WARP_NPC,4,4
1@sthd,110,110,0	duplicate(#pantheon_damage1)	#pantheon_damage4	HIDDEN_WARP_NPC,4,4
1@sthd,113,117,0	duplicate(#pantheon_damage1)	#pantheon_damage5	HIDDEN_WARP_NPC,4,4
1@sthd,112,103,0	duplicate(#pantheon_damage1)	#pantheon_damage6	HIDDEN_WARP_NPC,4,4
1@sthd,104,103,0	duplicate(#pantheon_damage1)	#pantheon_damage7	HIDDEN_WARP_NPC,4,4
1@sthd,97,103,0	duplicate(#pantheon_damage1)	#pantheon_damage8	HIDDEN_WARP_NPC,4,4
1@sthd,92,97,0	duplicate(#pantheon_damage1)	#pantheon_damage9	HIDDEN_WARP_NPC,4,4
1@sthd,99,95,0	duplicate(#pantheon_damage1)	#pantheon_damage10	HIDDEN_WARP_NPC,4,4
1@sthd,108,95,0	duplicate(#pantheon_damage1)	#pantheon_damage11	HIDDEN_WARP_NPC,4,4
1@sthd,116,95,0	duplicate(#pantheon_damage1)	#pantheon_damage12	HIDDEN_WARP_NPC,4,4
1@sthd,112,87,0	duplicate(#pantheon_damage1)	#pantheon_damage13	HIDDEN_WARP_NPC,4,4
1@sthd,104,88,0	duplicate(#pantheon_damage1)	#pantheon_damage14	HIDDEN_WARP_NPC,4,4
1@sthd,95,87,0	duplicate(#pantheon_damage1)	#pantheon_damage15	HIDDEN_WARP_NPC,4,4
1@sthd,96,78,0	duplicate(#pantheon_damage1)	#pantheon_damage16	HIDDEN_WARP_NPC,4,4
1@sthd,94,72,0	duplicate(#pantheon_damage1)	#pantheon_damage17	HIDDEN_WARP_NPC,4,4
1@sthd,104,81,0	duplicate(#pantheon_damage1)	#pantheon_damage18	HIDDEN_WARP_NPC,4,4
1@sthd,111,81,0	duplicate(#pantheon_damage1)	#pantheon_damage19	HIDDEN_WARP_NPC,4,4
1@sthd,114,72,0	duplicate(#pantheon_damage1)	#pantheon_damage20	HIDDEN_WARP_NPC,4,4
1@sthd,100,71,0	duplicate(#pantheon_damage1)	#pantheon_damage21	HIDDEN_WARP_NPC,4,4
1@sthd,91,101,0	duplicate(#pantheon_damage1)	#pantheon_damage22	HIDDEN_WARP_NPC,4,4
1@sthd,117,101,0	duplicate(#pantheon_damage1)	#pantheon_damage23	HIDDEN_WARP_NPC,4,4
1@sthd,117,90,0	duplicate(#pantheon_damage1)	#pantheon_damage24	HIDDEN_WARP_NPC,4,4
1@sthd,90,90,0	duplicate(#pantheon_damage1)	#pantheon_damage25	HIDDEN_WARP_NPC,4,4
// 1@sthd,100,71,0	duplicate(#pantheon_damage1)	#pantheon_damage23	HIDDEN_WARP_NPC,4,4
// 1@sthd,91,101,0	duplicate(#pantheon_damage1)	#pantheon_damage24	HIDDEN_WARP_NPC,4,4
// 1@sthd,117,101,0	duplicate(#pantheon_damage1)	#pantheon_damage25	HIDDEN_WARP_NPC,4,4
// 1@sthd,117,90,0	duplicate(#pantheon_damage1)	#pantheon_damage26	HIDDEN_WARP_NPC,4,4
// 1@sthd,90,90,0	duplicate(#pantheon_damage1)	#pantheon_damage27	HIDDEN_WARP_NPC,4,4

1@sthd,1,1,0	script	#stefan_boss_event	HIDDEN_WARP_NPC,{
	end;
OnBossDead:
	if (mobcount( 'sthd_map$, instance_npcname("#stefan_boss_event") + "::OnBossDead" ) < 1) {
		donpcevent instance_npcname("#Stefan for display3") + "::OnStop";
		donpcevent instance_npcname("#stefan_boss_zombie") + "::OnStop";
		donpcevent instance_npcname("#stefan_boss_cursed") + "::OnStop";
		donpcevent instance_npcname("#stefan_boss_talk") + "::OnStop";
		donpcevent instance_npcname("#stefan_boss_status") + "::OnStop";
		mapannounce 'sthd_map$, "Stefan Jack Earnest Wolf: Dead...I was...But...I won't be dead...", bc_map,0xEBFF;
		initnpctimer;
	}
	end;
OnTimer3000:
	stopnpctimer;
	mapannounce 'sthd_map$, "Stefan Jack Earnest Wolf: Comes back...the power of Bijou...Eternal body...Again...", bc_map,0xEBFF;
	donpcevent instance_npcname("Sky Fortress Escape Warp") + "::OnEnable";
	disablenpc instance_npcname("#stefan_boss_event");
	end;
}

1@sthd,1,1,0	script	#stefan_boss_talk	HIDDEN_WARP_NPC,{
	end;
OnTalk:
	enablenpc instance_npcname("#stefan_boss_talk");
	initnpctimer;
	end;
OnTimer6000:
	.@r = rand(100);
	if (.@r < 10)
		unittalk 'boss_id, "Jack: I'm hurt...I'm scared...I take courage...I hit you...";
	else if (.@r < 20)
		unittalk 'boss_id, "Wolf: Don't get away...Come closer...I'll kill you...";
	else if (.@r < 30)
		unittalk 'boss_id, "Earnest: I'm angry...It's noisy...I'll wipe them all up...";
	else
		unittalk 'boss_id, "Stefan: ~Groans~ ~Growls~";
	end;
OnStop:
	stopnpctimer;
	disablenpc instance_npcname("#stefan_boss_talk");
	end;
}

1@sthd,104,96,4	script	Sky Fortress Escape Warp	1_SHADOW_NPC,{
	if (select( "Do not go outside of the Sky Fortress.", "Go outside of the Sky Fortress." ) == 1) {
		mes "- The warp is emanating bright lights. -";
		close;
	}
	mes "- You start to transport. -";
	close2;
	warp "prt_q",244,81;
	end;

OnEnable:
	initnpctimer;
	enablenpc instance_npcname("Sky Fortress Escape Warp");
	end;
OnTimer3000:
	specialeffect EF_ENHANCE;
	initnpctimer;
	end;

OnInstanceInit:
	'sky_status = 0;
	'status_event = 0;
	'sthb_map$ = instance_mapname("1@sthb");
	'sthc_map$ = instance_mapname("1@sthc");
	'sthd_map$ = instance_mapname("1@sthd");

	// warps
	disablenpc instance_npcname("#sthd_move_01_01");
	disablenpc instance_npcname("#sthd_move_02_01");
	disablenpc instance_npcname("#sthd_move_02_02");

	disablenpc instance_npcname("Stefan.J.E.Wolf#sthd_02");
	// disablenpc instance_npcname("#Stefan_Action1");

	for ( .@i = 1; .@i <= 6; .@i++ ) {
		disablenpc instance_npcname("Activated Warp#sthd_mov_" + .@i);
		disablenpc instance_npcname("Desactivated Warp#sthd_mov_" + .@i);
		disablenpc instance_npcname("#sthd_move_back_a_" + .@i);
	}

	disablenpc instance_npcname("#sthd_event_2");
	disablenpc instance_npcname("#sthd_event_3");
	disablenpc instance_npcname("#sthd_event_4");
	for ( .@i = 5; .@i <= 18; .@i++ )
		disablenpc instance_npcname("#sthd_event_" + .@i);

	disablenpc instance_npcname("#sthd_key_control");

	disablenpc instance_npcname("#chest_spawn");
	disablenpc instance_npcname("Sky Fortress Gold Treasure#1");
	disablenpc instance_npcname("Sky Fortress Gold Treasure#2");
	disablenpc instance_npcname("Sky Fortress Gold Treasure#3");
	disablenpc instance_npcname("Sky Fortress Gold Treasure#4");
	disablenpc instance_npcname("Sky Fortress Treasure B");
	disablenpc instance_npcname("Wind Ghost in Experiment");
	disablenpc instance_npcname("Immortal Cursed Knight#");
	disablenpc instance_npcname("Immortal Wind Ghost#01");

	disablenpc instance_npcname("#Stefan_Action2");
	disablenpc instance_npcname("Stefan.J.E.Wolf#sthd_03");
	disablenpc instance_npcname("#Stefan for display3");
	disablenpc instance_npcname("Stefan.J.E.Wolf#sthd_04");
	disablenpc instance_npcname("#stefan_boss_event");
	disablenpc instance_npcname("#stefan_boss_talk");
	disablenpc instance_npcname("#stefan_boss_status");
	disablenpc instance_npcname("Sky Fortress Escape Warp");
	for ( .@i = 1; .@i <= 25; .@i++ )
		disablenpc instance_npcname("#pantheon_damage" + .@i);
	for ( .@i = 1; .@i <= 25; .@i++ )	// hideonnpc to display the effect
		hideonnpc instance_npcname(.@i + "#pantheon_damage");
	end;
}
